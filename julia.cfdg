width = 1600
cutoff = 1600
//c = -0.1, 0.651
//c = -0.8, 0.156
//c = -0.835, -0.2321
phi = (1 + sqrt(5)) / 2
//c = (phi-2), (phi-1)
c = 0.285, 0

startshape julia [b 1 sat 1]

shape julia {
  //loop xx = (-1.5, 1.5, 3/width) [x 1] {
  //  loop yy = (-1.5, 1.5, 3/width) [y 1] {
  //loop xx = (-.1, 0.2, .3/width) [x 1] {
  //  loop yy = (-0.4, -0.1, .3/width) [y 1] {
  loop xx = (-.65, -.35, .3/width) [x 1] {
    loop yy = (0.05, .35, .3/width) [y 1] {
      pixel(xx, yy) []
    }
  }
}

shape pixel(number real, number imaginary) {
  i = iterations(real, imaginary, 0)
  SQUARE [hue mod(i,360) b -floor(i/cutoff)]
}

iterations(real, imaginary, depth) = let (
  next_real = real^2 - imaginary^2 + c[0];
  next_imaginary = 2*real*imaginary + c[1];
  if (depth >= cutoff,
    cutoff,
    if (next_real^2 + next_imaginary^2 > 4,
      depth + 1,
      iterations(next_real, next_imaginary, depth + 1)
    )
  )
)

